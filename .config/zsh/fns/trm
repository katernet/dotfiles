# Trim prompt directory path
# Adapted from https://www.zsh.org/mla/workers/2009/msg00415.html
trm() {
	setopt localoptions rc_quotes null_glob
	unsetopt localoptions case_glob

	typeset -i i
	typeset -a tree expn
	typeset result part dir=${1-$PWD}

	[[ -d $dir ]] || return 0

	for part in ${(k)nameddirs}; do
		[[ $dir == ${nameddirs[$part]}(/*|) ]] && dir=${dir/#${nameddirs[$part]}/\~$part}
	done

	dir=${dir/#$HOME/\~}

	tree=(${(s:/:)dir})
	if [[ $tree[1] == \~* ]]; then
		cd -q ${~tree[1]}
		result=$tree[1]
		shift tree
	else
		cd -q /
	fi

	for dir in $tree; do
		if (( $#tree == 1 )); then
			result+="/$tree"
			break
		fi
		expn=(a b)
		part=''
		i=0
		until [[ (( ${#expn} == 1 )) || $dir = $expn || $i -gt 99 ]]; do
			(( i++ ))
			part+=$dir[$i]
			expn=(${part}*(-/))
		done
		result+="/$part"
		cd -q $dir
		shift tree
	done

	print ${result:-/}
}
